#[===============================================[/*
This is part of the TWX build and test system.
https://github.com/TeXworks/texworks
(C)  JL 2023
*/
/** @file
  * @brief TwxMessageLib test suite.
  *
  *//*
#]===============================================]

include_guard ( GLOBAL )

twx_test_suite_push ()
block ()

twx_test_unit_push ( CORE "log_level_index" )
if ( TWX_TEST_UNIT.RUN )
  block ()
  set ( i 0 )
  foreach ( x ${TWX_MESSAGE_LOG_LEVELS} )
    twx_message_log_level_index ( ${x} IN_VAR ans )
    twx_expect_equal_number ( "${ans}" "${i}" )
    math ( EXPR i "${i}+1" )
  endforeach ()
  endblock ()
endif ()
twx_test_unit_pop ()

twx_test_unit_push ( CORE "log_level_order" )
if ( TWX_TEST_UNIT.RUN )
  block ()
  twx_message_log_level_order ( NOTICE <=> TRACE IN_VAR ans )
  twx_expect_equal_number ( "${ans}" -1 )
  twx_message_log_level_order ( NOTICE <=> NOTICE IN_VAR ans )
  twx_expect_equal_number ( "${ans}" 0 )
  twx_message_log_level_order ( TRACE <=> NOTICE IN_VAR ans )
  twx_expect_equal_number ( "${ans}" 1 )
  twx_fatal_test ()
  twx_message_log_level_order ( NoTICE <=> TRACE IN_VAR ans )
  twx_fatal_assert_fail ()
  twx_fatal_test ()
  twx_message_log_level_order ( NOTICE <=> TRaCE IN_VAR ans )
  twx_fatal_assert_fail ()
  endblock ()
endif ()
twx_test_unit_pop ()

twx_test_unit_push ( CORE "log_level_compare" )
if ( TWX_TEST_UNIT.RUN )
  block ()
  foreach ( level ${TWX_MESSAGE_LOG_LEVELS} )
    set ( op ">" )
    foreach ( l ${TWX_MESSAGE_LOG_LEVELS} )
      if ( "${l}" STREQUAL "${level}" )
        twx_message_log_level_compare ( "${l}" "==" "${level}" IN_VAR ans )
        twx_assert_true ( ans )
        twx_message_log_level_compare ( "${l}" "="  "${level}" IN_VAR ans )
        twx_assert_true ( ans )
        twx_message_log_level_compare ( "${l}" "!=" "${level}" IN_VAR ans )
        twx_assert_false ( ans )
        twx_message_log_level_compare ( "${l}" "<>" "${level}" IN_VAR ans )
        twx_assert_false ( ans )
        set ( op "<" )
        continue ()
      endif ()
      # # message ( TR@CE "${level} ${op} ${l}" )
      twx_message_log_level_compare ( "${level}" "${op}" "${l}" IN_VAR ans )
      twx_assert_true ( ans )
    endforeach ()
  endforeach ()
  endblock ()
endif ()
twx_test_unit_pop ()

twx_test_unit_push ( CORE prettify )
if ( TWX_TEST_UNIT.RUN )
  block ()
  twx_message_log ( STATUS "THIS IS A TEST")
  twx_fatal_test ()
  twx_message_register_prettifier ( TwxMessageTest )
  twx_fatal_assert_fail ()
  function ( TwxMessageTest_prettify m IN_VAR v )
    # # message ( TR@CE "Ugly: ${m}")
    string ( REPLACE "a" "A" m "${m}" )
    # # message ( TR@CE "Pretty: ${m}")
    set ( ${v} "${m}" PARENT_SCOPE )
  endfunction ()
  twx_fatal_test ()
  twx_message_register_prettifier ( TwxMessageTest )
  twx_fatal_assert_pass ()
  twx_message_prettify ( "b" "a" "c" IN_VAR msg )
  twx_expect_equal_string ( "${msg}" "b;A;c" )
  twx_expect ( msg "b;A;c" )
  twx_fatal_test ()
  twx_message_unregister_prettifier ( TwxMessageTest )
  twx_fatal_assert_pass ()
  twx_message_prettify ( "b" "a" "c" IN_VAR msg )
  twx_expect_equal_string ( "${msg}" "b;a;c" )
  endblock ()
endif ()
twx_test_unit_pop ()

endblock ()
twx_test_suite_pop ()

#/*
